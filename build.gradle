plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply from: rootProject.file('extras.gradle')

group = GROUP
version = VERSION

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()

    maven {
        url('https://frcmaven.wpi.edu/artifactory/release')
    }

    mavenLocal() //remove
}

dependencies {
    api group: 'com.flash3388.flashlib', name: 'flashlib', version: '2.2.0'

    api group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: '2020.1.2'
    api group: 'edu.wpi.first.hal', name: 'hal-java', version: '2020.1.2'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.7.22'
}

jar {
    archiveBaseName.set('flashlib-frc')
}

task createSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource

    archiveBaseName.set('flashlib-frc')
    archiveClassifier.set('sources')
}

task createJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir

    archiveBaseName.set('flashlib-frc')
    archiveClassifier.set('javadoc')
}

task flashlibFrcAll(type: Jar, dependsOn: build) {
    dependsOn createSources
    dependsOn createJavadoc

    archiveFileName.set('flashlib-frc-all.jar')
    destinationDirectory.set(rootProject.buildDir)

    from sourceSets.main.output
    from sourceSets.main.allSource
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenFlashlib(MavenPublication) {
            from components.java

            artifact createJavadoc
            artifact createSources

            pom {
                name = 'FlashFRC'
                description = 'FRC plugin for FlashLib'
                url = 'https://github.com/Flash3388/FlashFRC'

                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id = 'tomtzook'
                        name = 'Tom Tzook'
                        email = 'tomtzook@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/Flash3388/FlashFRC.git'
                    developerConnection = 'scm:git:ssh://github.com/Flash3388/FlashFRC.git'
                    url = 'https://github.com/Flash3388/FlashFRC'
                }
            }
        }
    }

    repositories {
        maven {
            if (isReleaseBuild()) {
                name = 'NexusStaging'
                url = NEXUS_RELEASE_REPOSITORY_URL
            } else {
                name = 'NexusSnapshot'
                url = NEXUS_SNAPSHOT_REPOSITORY_URL
            }

            credentials {
                username getNexusUsername()
                password getNexusPassword()
            }
        }
    }
}

if (shouldSign()) {
    signing {
        sign publishing.publications.mavenFlashlib
    }
}